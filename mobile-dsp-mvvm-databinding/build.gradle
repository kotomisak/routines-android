apply from: '../common-app.gradle'
apply from: '../common.gradle'
//apply from: '../jacoco.gradle' //since app module doesn't apply module-core/library.gradle, thus apply jacoco.gradle extra

def versionNameParam = "git describe --tags --abbrev=0".execute().text.trim()
def versionCodeParam = "git tag --list".execute().text.split("\n").size()
def todayParam = new Date().format('yyyyMMdd')

android {

    compileSdkVersion versions.sdk
    defaultConfig {
        applicationId 'cz.kotox.dsp'
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.sdk
        versionCode versionCodeParam
        versionName versionNameParam
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue "string", "app_name", "DSP"
        setProperty("archivesBaseName", "DSP-${versionNameParam}-${versionCodeParam}-${todayParam}")
        buildConfigField "int", "DSP_VERSION_CODE", "${versionCodeParam}"
        buildConfigField "String", "DSP_VERSION_NAME", "\"${versionNameParam}\""
    }

    buildTypes {
        release {
            resValue("bool", "firebase_analytics_deactivated", "false")
            signingConfig signingConfigs.release
        }
        debug {
            resValue("bool", "firebase_analytics_deactivated", "true")
            signingConfig signingConfigs.debug
            ext.alwaysUpdateBuildId = false
        }
    }
    dataBinding {
        enabled = true
    }
    flavorDimensions "env"

    productFlavors {
        mock {
            dimension = "env"
            applicationIdSuffix ".mock"
            resValue "string", "app_name", "DSP-M"
        }

        dev {
            dimension = "env"
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "DSP-D"
        }

        production {
            dimension = "env"
            applicationIdSuffix ".prod"
            resValue "string", "app_name", "DSP"
        }
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {

    implementation project(':core')
    implementation project(':core-analytics')
    implementation project(':core-arch-mvvm-databinding-nav')
    implementation project(':core-dsp')
    implementation project(':core-rest')
    implementation project(':core-media')
    implementation project(':core-view')



}